/*
Example project to build and run a Notes application on your local machine.
*/
plugins {
    id 'java'
    id 'application'  // allows 'run' task.
    id 'eclipse'      // allows dependencies to be exported to .classpath
}
mainClassName = 'JavaAddinCoreDemo'
archivesBaseName='JavaAddinCoreDemo'

String notesInstallation = project.findProperty('notesInstallation')
if (!notesInstallation) {
	throw new GradleException("Missing configured path for Notes installation.  Set notesInstallation in gradle.properties.")
}
else if (!(new File(notesInstallation).exists())) {
	throw new GradleException("Invalid configured path for Notes installation ($notesInstallation).  Check notesInstallation in build.gradle.")
}

String notesJarExtDir = "$notesInstallation/jvm/lib/ext"
String notesJarPath = "$notesJarExtDir/Notes.jar"
// change envPath as needed
if (notesInstallation.toLowerCase().startsWith('/applications/')) { // treat as macOS
	if (!(new File(notesJarPath).exists())) {
		// alternative location for macOS:  /Applications/HCL Notes.app/Contents/Resources/jvm/lib/ext/Notes.jar 
		logger.info "Notes.jar not found at $notesJarPath"
		notesJarExtDir = "$notesInstallation/../Resources/jvm/lib/ext"
		notesJarPath = "$notesJarExtDir/Notes.jar"
	}
	if (!(new File(notesJarPath).exists())) {
		logger.info "Notes.jar not found at $notesJarPath"
		throw new GradleException("ERROR: Could not find Notes.jar")
	}
	logger.info("Notes JAR path:  $notesJarPath")
}
// no special logic for Windows

// build a jar path to use for Notes.jar
// Supports local and Linux server executions
String jarClassPath = "./Notes.jar /opt/hcl/domino/notes/latest/linux/jvm/lib/ext/Notes.jar ${notesJarPath.replaceAll(' ', '%20').replaceAll('^\\w+:', '/$0')}" 

logger.debug "Environment"
System.env.each {
    logger.debug "${it.key}:${it.value}"
}

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

// Required for Java 8 Language server support
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // Local JAR dependency
    implementation files(notesJarPath)
    
    // NOTE: Add any additional dependencies (local or Maven) here.  They will be automatically bundled in the JAR:
    //implementation files('lib/myJar.jar')
    //implementation 'org.json:json:20160212'  // Maven:  Format is 'groupid:name:version'
    
}


/*
 * Build a fat jar (excluding Notes.jar) for import into script library.
 */
jar {
	from { 
		// runtimeClasspath includes implementation and runtimeOnly dependencies
		// findResults allows using null to exclude results
		configurations.runtimeClasspath.findResults { File dependency ->
			if (dependency.getName().equalsIgnoreCase("Notes.jar")) {
				return null  // skip this entry
			}
			else {
				return dependency.isDirectory() ? dependency : zipTree(dependency) 
			}
		} 
	}
	// classpath needs to be specified in individual tasks
}


